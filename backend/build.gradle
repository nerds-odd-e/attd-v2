apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'war'
apply plugin: "com.github.erdi.webdriver-binaries"
apply plugin: "io.spring.dependency-management"

sourceCompatibility = 8

repositories {
    mavenCentral()
}

configurations {
    providedRuntime
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

sourceSets {
    main {
        resources {
            srcDir '../frontend/build/dist'
        }
    }
}

processResources.dependsOn ':frontend:npmBuild'

ext {
    set('springCloudVersion', "2020.0.2")
    set('jfactory', '0.1.0')
    set('jfactoryCucumber', '0.1.2')
    set('jfactoryRepoJpa', '0.1.2')
    set('jsonAssertExtendComparator', '0.1.13')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    annotationProcessor('org.projectlombok:lombok:1.18.18')
    testAnnotationProcessor('org.projectlombok:lombok:1.18.18')
    compile('org.projectlombok:lombok:1.18.18')
    providedRuntime('org.springframework.boot:spring-boot-starter-tomcat')
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("mysql:mysql-connector-java:8.0.13")
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation("io.cucumber:cucumber-java:6.10.2")
    testImplementation("io.cucumber:cucumber-spring:6.10.2")
    testImplementation("org.assertj:assertj-core:3.19.0")
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59'
    testImplementation "org.awaitility:awaitility:4.0.3"
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'
    testImplementation group: 'org.mock-server', name: 'mockserver-client-java', version: '5.4.1'
    testImplementation(platform('org.junit:junit-bom:5.7.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.9.0'
    testImplementation group: 'io.appium', name: 'java-client', version: '7.5.1'
    testImplementation "com.github.leeonky:JSONassert-extend-comparator:${jsonAssertExtendComparator}"
    testImplementation "com.github.leeonky:jfactory:${jfactory}"
    testImplementation "com.github.leeonky:jfactory-cucumber:${jfactoryCucumber}"
    testImplementation "com.github.leeonky:jfactory-repo-jpa:${jfactoryRepoJpa}"
    testImplementation "org.reflections:reflections:0.9.12"
    testImplementation 'com.github.leeonky:bean-util:0.2.2'
}

test {
    useJUnitPlatform()
}

task cucumber(type: JavaExec) {
    dependsOn assemble, testClasses
    main = "io.cucumber.core.cli.Main"
    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
    environment "SPRING_PROFILES_ACTIVE", "test"
    args = ['--plugin', 'pretty',
            '--glue', 'com.odde.atddv2',
            '--glue', 'com.github.leeonky.jfactory.cucumber',
            '--tags', 'not @android',
            'src/test/resources']
}

webdriverBinaries {
    chromedriver {
        version = '90.0.4430.24'
        fallbackTo32Bit = true
    }
    driverUrlsConfiguration = resources.text.fromFile('webdriver_download_config.json')
    configureTask(cucumber)
}

bootRun {
    environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: "dev"
}
